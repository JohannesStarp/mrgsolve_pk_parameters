$PROBLEM PK model
;; 1. Based on: run006
;; 2. Description: 1_CMT_IIV_EXP_WT_on_V_exp_ADVAN13
;; x1. Author: Johan
;; 3. Label: LZD_Swoboda ; http://link.springer.com/10.1007/s00228-009-0766-9

$INPUT
ID TIME TAD EVID MDV DV ROUTE AMT DUR SEX WT CLCR SCR AGE


$DATA nm_dataset_covariates.csv IGNORE=@;
IGNORE(ROUTE.EQ.3); only IV Data

$SUBROUTINES ADVAN13 TOL=6

$MODEL 
COMP = (CENTRAL)
$PK
TVCL = THETA(1)
CL = TVCL * EXP( ETA (1))
TVV1 = THETA(2) * (WT/75)**THETA(3)
V = TVV1 * EXP( ETA (2))

S1 = V

$DES
DADT(1)= -CL/V * A(1)

$THETA
(0, 5.86) ;1 CL
(0, 55.1) ;2 V1
(0, 1.14) ;WT on V1

$OMEGA
 0.29 ; 1 IIV CL
 0.00536 ; 2 IIV V1

$SIGMA
 0.0209

$ERROR
Y = F + F * EPS(1) ; proportional
IPRED = F
W = SQRT(SIGMA(1)*F*F)
IRES = DV - IPRED
IWRES = IRES /  W

$SIM (12345) SUBPROBLEMS=1000 ONLYSIM
; $COV PRINT = E
; 
; $ESTIMATION METHOD=1 INTERACTION MAXEVAL=1000 SIG=3 PRINT=1 NOABORT POSTHOC
$TABLE ID TIME EVID MDV DV PRED IPRED CWRES WRES IWRES ONEHEADER NOAPPEND NOPRINT FILE=sdtab007

$TABLE ID CL V ETA1 ETA2
NOPRINT ONEHEADER FILE=patab007 ; Model parameters - THETAs, ETAs and EPSs

$TABLE ID WT CLCR SCR AGE
NOPRINT ONEHEADER FILE=cotab007; Continous covariates

$TABLE ID SEX
NOPRINT ONEHEADER FILE=catab007 ; Categorical covariates

